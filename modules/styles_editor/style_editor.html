<!DOCTYPE html>

<html lang="en">
<head>
	<meta charset="UTF-8">
	<title>ProffieOS Style Editor</title>
	<script type="application/javascript" src="script.js"></script>
	<script id="vertex_shader" type="x-shader/x-vertex">
		precision highp float;
		attribute vec2 a_position;
		varying vec2 v_position;
		uniform float u_width;
		uniform float u_height;

		void main() {
			v_position = a_position * vec2(u_width, u_height);
			gl_Position = vec4(a_position, 0, 1);
		}
	</script>

	<script id="hilt_graflex" type="x-shader/x-fragment">

		#define ITERATIONS 400

float Clamp(vec3 p) {
  p= vec3(p.x + 0.03, -p.z, p.y);
  float dist = 100000.0;
  // clamp
  dist = min(dist, sdCappedCylinder(p.yxz, 0.040, 0.051));
  dist = min(dist, sdBox(p.xyz + vec3(0.0, -0.038, 0.0), vec3(0.051, 0.020, 0.015)));
  return dist;
}

float RedPill(vec3 p) {
  p -= vec3(0.143,0,0);
  if (p.y > 0.0) {
    p.x-=0.030;
    p.y = abs(p.y);
  }
  float dist = 1000000.0;
  //  dist = min(dist, sdCappedCylinder(p, 0.011, 0.051));
  dist = min(dist,
      max(sdSphere(p, 0.053),
      sdCappedCylinder(p, 0.011, 0.08)));
  return dist;
}

float Button(vec3 p) {
  float dist = 1000000.0;
  dist = min(dist, RedPill(p));
  p -= vec3(0.143,0,0);
  if (p.y > 0.0) {
    p.x-=0.030;
    p.y = abs(p.y);
  }
  dist = min(dist, sdCappedCylinder(p, 0.018, 0.050));
  //  dist = min(dist, sdCappedCylinder(p, 0.011, 0.051));
  return dist;
}

// May need to find a better way to model this.
float Cut(vec3 p) {
  p.x -= 0.350;
  //  p.y -= 0.008;
  p *= rotz(-0.60);
  p.x += sin(p.y*50.0)/55.0;
  return sdBox(p, vec3(0.1, 0.2, 0.1)) * 0.8;
}

float Slots(vec3 p) {
  float dist = sdBox(p + vec3(-0.259,0.0,0.0), vec3(0.004, 0.1, 0.005));
  dist = min(dist, sdBox(p + vec3(-0.227,0.0,0.0), vec3(0.020, 0.1, 0.010)));
  return dist;
}

float Slots2(vec3 p) {
  p.z = -abs(p.z);
  //  float dist = 100000.0;
  float dist = sdBox(p + vec3(-0.179,0.055,0.01), vec3(0.010, 0.03, 0.005));
  p *= rotx(0.95);
  dist = min(dist, sdCappedCylinder(p+vec3(-0.175,0.040,0), 0.014, 0.02));
  //  dist = min(dist, sdBox(p + vec3(-0.227,0.0,0.0), vec3(0.020, 0.1, 0.010)));
  return dist;
}

float Pins(vec3 p) {
  p.z = -abs(p.z);
  p *= rotx(0.95);
  p += vec3(-0.175,0.015,0);
  p.x = abs(p.x);
  p.x -= 0.005;
  return sdVerticalCapsule(p.yxz, 0.018, 0.002);
  //  return sdCappedCylinder(p, 0.002, 0.02);
}


float Cylinder(vec3 p) {
  float dist = sdCappedCylinder(p.yxz, 0.038, 0.270);
  dist = max(dist, -sdCappedCylinder(p.yxz + vec3(0,-0.470,0), 0.036, 0.270));
  dist = max(dist, -Cut(p));
  dist = max(dist, -Slots(p));
  dist = max(dist, -Slots2(p));
  return dist;
}

float sdCircle(vec2 xy, float r) {
  return length(xy) - r;
}

float sdRect(vec2 p, vec2 b) {
  vec2 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,q.y),0.0);
}

float earsFront(vec2 xy) {
  xy.x = -abs(xy.x);
  float dist = sdCircle(xy, 0.027);
  dist = max(dist, -sdCircle(xy, 0.0265));
  dist = max(dist, -sdRect(xy + vec2(0.0, -0.030), vec2(0.005, 0.010)));
  dist = min(dist, sdRect(xy + vec2(0.005, -0.056), vec2(0.0005, 0.030)));
  return dist;
}

float earsSide(vec2 xy) {
  xy.x = -abs(xy.x);
  float dist = sdRect(xy + vec2(0, -0.050), vec2(0.012, 0.030));
  dist = min(dist, sdRect(xy, vec2(0.016, 0.030)));
  dist = min(dist, max(
     sdCircle(xy + vec2(0,-0.069), 0.016),
     -sdRect(xy + vec2(0.011*2.0, -0.060), vec2(0.011, 0.030))));
  return dist;
}


float bunnyEars(vec3 p) {
  p.x -= 0.227;
  return max(
      opExtrusion( p.zyx, earsFront(p.zy), 0.1 ),
      opExtrusion( p, earsSide(p.xy), 0.1 ));
}

float ROUND(float x) {
  return floor(x + 0.5);
}

float grips(vec3 p) {
  // 6-symmetry
  float angle = atan(p.z, p.y);
  float section = ROUND(angle * 3.0 / PI);
  float angle2 = section * PI / 3.0;
  vec3 p2 = p * rotx(angle2);

  // T-track
  float dist = sdBox(p2.xyz + vec3(0.270-0.092,-0.038,0.0), vec3(0.092, 0.004, 0.013));
  dist = min(dist, sdBox(p2.xyz + vec3(0.270-0.092,-0.038,0.0), vec3(0.091, 0.012, 0.002)) - 0.001);
  return dist;
}

#define SCALE 0.25

// FIXME
vec3 dorot(vec3 p) {
  p.y -= 3.75;
  p *= SCALE;
  p = p.yxz;
  p.x = -p.x;
  p *= rotx(-PI/2.0);

//  p = p  * roty(-0.4) * rotx(PI/5.0);
  //    p = p  * roty(-0.4) * rotx(PI/7.0);
  //    p = p * roty(-0.05);
//  p *= rotz(iTime*0.1) * rotx(-iTime*0.0123) * roty(iTime*0.09781623);
  return p;
}

// bounding box
float bb(vec3 p) {
  p.y-=0.01;
  return sdVerticalCapsule(p, 0.25, 0.08);
}

// Return actual distance, step
float handle2(vec3 p) {
  p = dorot(p);

  float dist = bb(p);
  if (dist > 0.1) return dist;

  dist = 100000.0;
  dist = min(dist, Cylinder(p));
  dist = min(dist, Clamp(p));
  dist = min(dist, Button(p));
  dist = min(dist, grips(p));
  dist = min(dist, bunnyEars(p));
  dist = min(dist, Pins(p));

  return dist;
}

float handle(vec3 p) {
  return handle2(p) / SCALE;
}

Material getHiltMaterial(vec3 hp, vec3 ray_dir) {
  float dist = handle2(hp);
  vec3 hpr = dorot(hp);

  if (grips(hpr) == dist) {
    return Material(vec3(0.01,0.01,0.01), 0.0, 0.4, vec3(0));
  }

  if (Pins(hpr) == dist) {
    return Material(vec3(1.00, 0.71, 0.29), 1.0, 0.2, vec3(0));
  }

  if (length(hpr.zy) < 0.036 && abs(hpr.x) < 0.201) {
    return Material(vec3(0.01,0.01,0.01), 0.2, 0.2, vec3(0));
  }

  if (RedPill(hpr) == dist) {
    return Material(vec3(0.6,0.1,0.1), 0.5, 0.2, vec3(0));
  }

  if (hpr.z < -0.055 && abs(hpr.y) < 0.011) {
    if (mod(hpr.x+0.2, 0.006) < 0.002) {
      return Material(vec3(0.3, 0.9, 0.3), 0.0, 0.4, vec3(0));
    } else {
      return Material(vec3(1.00, 0.71, 0.29), 1.0, 0.2, vec3(0));
    }
  }

  return Material(vec3(1.0), 0.9, 0.3, vec3(0));
//  vec2 tpos = vec2(dot(hpr, vec3(13.1,1,0.5)), dot(hpr, vec3(7.033,0.1,2.2)));
//  vec3 c = texture(iChannel0, tpos).xyz;
//  return Material(vec3(0.3), 1.0-c.g, c.r*0.8 + 0.1);
}



	</script>

	<script id="hilt_cylinder" type="x-shader/x-fragment">

		#define ITERATIONS 100

float handle(vec3 p) {
   vec2 h = vec2(.15, 0.7);
   p.y -= 3.65;
   vec2 d = abs(vec2(length(p.xz),p.y)) - h;
   return min(max(d.x,d.y),0.0) + length(max(d,0.0));
}

Material getHiltMaterial(vec3 hp, vec3 ray_dir) {
  return Material(vec3(1.0), 0.9, 0.3, vec3(0));
}

	</script>

	<script id="fragment_shader" type="x-shader/x-fragment">
		precision highp float;
uniform float u_time;
varying vec2 v_position;
uniform float u_width;
uniform float u_height;
uniform sampler2D sampler;
uniform mat4 u_move_matrix;
uniform mat4 u_old_move_matrix;

const float PI = 3.1415926535;

struct Material {
  vec3 color;
  float metallic;
  float roughness;
  vec3 emission;
};

$VARIABLES$

mat3 rotz(float f) {
  return mat3(cos(f), sin(f), 0,
              -sin(f), cos(f), 0,
              0,0,1);
}

mat3 roty(float f) {
  return mat3(cos(f), 0, sin(f),
              0, 1, 0,
              -sin(f), 0, cos(f));
}

mat3 rotx(float f) {
  return mat3(1,0,0,
              0, cos(f), sin(f),
              0, -sin(f), cos(f));
}



float opExtrusion( in vec3 p, in float sdf, in float h) {
  vec2 w = vec2( sdf, abs(p.z) - h );
  return min(max(w.x,w.y),0.0) + length(max(w,0.0));
}

float sdBox( vec3 p, vec3 b) {
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}

float sdSphere( vec3 p, float s) {
  return length(p)-s;
}

float sdCylinder( vec3 p, vec3 c) {
  return length(p.xz-c.xy)-c.z;
}

float sdCappedCylinder( vec3 p, float h, float r) {
  vec2 d = abs(vec2(length(p.xz),p.y)) - vec2(h,r);
  return min(max(d.x,d.y),0.0) + length(max(d,0.0));
}

float sdRoundedCylinder( vec3 p, float ra, float rb, float h) {
  vec2 d = vec2( length(p.xz)-2.0*ra+rb, abs(p.y) - h );
  return min(max(d.x,d.y),0.0) + length(max(d,0.0)) - rb;
}

float sdVerticalCapsule( vec3 p, float h, float r )
{
  p.x -= clamp( p.x, -h, h );
  return length( p ) - r;
}

vec4 opElongate( in vec3 p, in vec3 h) {
  return vec4( p-clamp(p,-h,h), 0.0 ); // faster, but produces zero in the interior elongated box

  //vec3 q = abs(p)-h;
  //return vec4( max(q,0.0), min(max(q.x,max(q.y,q.z)),0.0) );
}




//------------------------------------------------------------------------------
// BRDF
//------------------------------------------------------------------------------

float pow5(float x) {
  float x2 = x * x;
  return x2 * x2 * x;
}

float D_GGX(float linearRoughness, float NoH, const vec3 h) {
  // Walter et al. 2007, "Microfacet Models for Refraction through Rough Surfaces"
  float oneMinusNoHSquared = 1.0 - NoH * NoH;
  float a = NoH * linearRoughness;
  float k = linearRoughness / (oneMinusNoHSquared + a * a);
  float d = k * k * (1.0 / PI);
  return d;
}

float V_SmithGGXCorrelated(float linearRoughness, float NoV, float NoL) {
  // Heitz 2014, "Understanding the Masking-Shadowing Function in Microfacet-Based BRDFs"
  float a2 = linearRoughness * linearRoughness;
  float GGXV = NoL * sqrt((NoV - a2 * NoV) * NoV + a2);
  float GGXL = NoV * sqrt((NoL - a2 * NoL) * NoL + a2);
  return 0.5 / (GGXV + GGXL);
}

vec3 F_Schlick(const vec3 f0, float VoH) {
  // Schlick 1994, "An Inexpensive BRDF Model for Physically-Based Rendering"
  return f0 + (vec3(1.0) - f0) * pow5(1.0 - VoH);
}

float F_Schlick(float f0, float f90, float VoH) {
  return f0 + (f90 - f0) * pow5(1.0 - VoH);
}

float Fd_Burley(float linearRoughness, float NoV, float NoL, float LoH) {
  // Burley 2012, "Physically-Based Shading at Disney"
  float f90 = 0.5 + 2.0 * linearRoughness * LoH * LoH;
  float lightScatter = F_Schlick(1.0, f90, NoL);
  float viewScatter  = F_Schlick(1.0, f90, NoV);
  return lightScatter * viewScatter * (1.0 / PI);
}

float Fd_Lambert() {
  return 1.0 / PI;
}

//------------------------------------------------------------------------------
// Indirect lighting
//------------------------------------------------------------------------------

vec3 Irradiance_SphericalHarmonics(const vec3 n) {
  // Irradiance from "Ditch River" IBL (http://www.hdrlabs.com/sibl/archive.html)
  return max(
     vec3( 0.754554516862612,  0.748542953903366,  0.790921515418539)
   + vec3(-0.083856548007422,  0.092533500963210,  0.322764661032516) * (n.y)
   + vec3( 0.308152705331738,  0.366796330467391,  0.466698181299906) * (n.z)
   + vec3(-0.188884931542396, -0.277402551592231, -0.377844212327557) * (n.x)
  , 0.0);
}

vec2 PrefilteredDFG_Karis(float roughness, float NoV) {
  // Karis 2014, "Physically Based Material on Mobile"
  const vec4 c0 = vec4(-1.0, -0.0275, -0.572,  0.022);
  const vec4 c1 = vec4( 1.0,  0.0425,  1.040, -0.040);

  vec4 r = roughness * c0 + c1;
  float a004 = min(r.x * r.x, exp2(-9.28 * NoV)) * r.x + r.y;

  return vec2(-1.04, 1.04) * a004 + r.zw;
}





vec3 A = vec3(0,3,0);
vec3 B = vec3(0,-4,0);

float get_point(vec3 p) {
   vec3 pa = p - A, ba = B - A;
   return clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
}

int get_led(vec3 p) {
  return int(get_point(p) * 144.0);
}

float blade2(vec3 p, bool tangent) {
   vec3 pa = p - A, ba = B - A;
   float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
   vec3 tmp = pa - ba * h;
   float l2 = dot(tmp, tmp);
   //   float R = 0.08;
   float R = 0.09;
    if (tangent && l2 >= R*R) {
     return sqrt(l2 - R*R);
   }  else {
     return sqrt(l2) - R;
   }
}

float blade(vec3 p, bool tangent) {
  float ret = blade2(p, tangent);
//  ret = min(ret, sdSphere(p, 0.8));
  return ret;
}
$HILT$

float map(vec3 p, bool tangent) {
  return min(blade(p, tangent), handle(p));
}

vec3 getBladeColor(vec3 pos) {
    return texture2D(sampler, vec2(get_point(pos), 0.5)).rgb;
}

float march(vec3 start, vec3 dir, out vec3 haze_color) {
  haze_color = vec3(0,0,0);
  float dist = 0.0;
  float last_dist = 0.0;
  float last_d = 0.0;
  bool hit = false;
  float haze = 1.0;
  for (int i = 0; i < ITERATIONS; i++) {
    vec3 p =start + dir * dist;
    float hilt_dist = handle(p);
    float blade_dist = blade(p, !hit);
    float d = min(hilt_dist, blade_dist);

    if (dist > 20.0) return 10000.0;
    if (d < 0.001) {
      if (d < 0.0) {
        hit = true;
//        dist = last_dist + (dist - last_dist) * (last_d / (last_d - d));
        dist = last_dist;
        continue;
      }
      return dist;
    }
    if (get_led(p) > 0) {
      // haze_color += getBladeColor(p) * clamp(0.002 / (blade_dist*blade_dist), 0.0, 1.0);

      // haze_color += getBladeColor(p) * 0.0005 / (blade_dist*blade_dist*blade_dist) * d;

      float haze_m = clamp(0.004 / (blade_dist*blade_dist), 0.0, 1.0);
      haze_color += getBladeColor(p) * haze_m * haze;
      haze *= (1.0 - haze_m);
    }
    d *= 0.99;
    last_d = d;
    last_dist = dist;
    dist += d;
  }
//  if (hit) return dist;
//  return -1.0;
  return dist;
}

float march2(vec3 start, vec3 dir, out vec3 haze_color) {
  haze_color = vec3(0,0,0);
  float dist = 0.0;
  float last_dist = 0.0;
  float last_d = 0.0;
  bool hit = false;
  float haze = 1.0;
  float start_dist = map(start, false);
  for (int i = 0; i < ITERATIONS; i++) {
    vec3 p =start + dir * dist;
    float hilt_dist = handle(p);
    float blade_dist = blade(p, !hit);
    float d = min(hilt_dist, blade_dist);

    if (dist > 20.0) return 10000.0;
    if (d < start_dist / 2.0) {
      if (d < 0.0) {
        hit = true;
//        dist = last_dist + (dist - last_dist) * (last_d / (last_d - d));
        dist = last_dist;
        continue;
      }
      return dist;
    }
    if (get_led(p) > 0) {
      // haze_color += getBladeColor(p) * clamp(0.002 / (blade_dist*blade_dist), 0.0, 1.0);

      // haze_color += getBladeColor(p) * 0.0005 / (blade_dist*blade_dist*blade_dist) * d;

      float haze_m = clamp(0.004 / (blade_dist*blade_dist), 0.0, 1.0);
      haze_color += getBladeColor(p) * haze_m * haze;
      haze *= (1.0 - haze_m);
    }
    last_d = d;
    last_dist = dist;
    dist += d;
  }
  return dist;
}



vec3 getNormal(vec3 p) {
  float E = 0.00001;
  vec3 X = vec3(E,0.0,0.0);
  vec3 Y = vec3(0.0,E,0.0);
  vec3 Z = vec3(0.0,0.0,E);
//  return normalize(vec3(map(p + X, false) - map(p - X, false),
//                        map(p + Y, false) - map(p - Y, false),
//                        map(p + Z, false) - map(p - Z, false)));

  float D = map(p, false);
  return normalize(vec3(map(p + X, false) - D,
                        map(p + Y, false) - D,
                        map(p + Z, false) - D));
}

float line_dist(vec3 pt1, vec3 dir1, vec3 pt2, vec3 dir2) {
  vec3 n = normalize(cross(dir1, dir2));
  return abs(dot(n, pt1 - pt2));
}



// Cast a ray starting at "from" and keep going until we hit something or
// run out of iterations.
float ray(vec3 from, vec3 direction) {
  // How far we travelled (so far)
  float travel_distance = 0.0;
  float last_travel_distance = 0.0;
  bool hit = false;
  for (int i = 0; i < 60; i++) {
    // calculate the current position along the ray
    vec3 position = from + direction * travel_distance;
    float tmp = map(position, false);
    float distance_to_closest_object = tmp;
    float step_size = hit ? tmp : tmp;

    if (distance_to_closest_object < 0.0005) {
      return travel_distance;
    }
    last_travel_distance = travel_distance;

    // We can safely advance this far since we know that the closest
    // object is this far away. (But possibly in a completely different
    // direction.)
    travel_distance += step_size;
  }

  return travel_distance;
}


float shadow(in vec3 origin, in vec3 direction) {
  float hit = 1.0;
  float t = 0.001;

  for (int i = 0; i < 100; i++) {
    float h = map(origin + direction * t, false);
    if (h < 0.0002) return 0.0;
    t += h;
    hit = min(hit, 10.0 * h / t);
    if (t >= 2.5) break;
  }

  return clamp(hit, 0.0, 1.0);
}



#define saturate(x) clamp(x, 0.0, 1.0)


Material getMaterial(vec3 hp, vec3 dir) {
  if (blade(hp, false) <= handle(hp)) {
    return Material(vec3(1.0), 0.0, 0.3, getBladeColor(hp));
  } else {
    return getHiltMaterial(hp, dir);
  }
}

void main() {
    vec3 light = vec3(-5, 8.0, -8.0);
    vec3 eye = vec3(0.0, 0.0, -12.0);
    float zoom = 1.5;
    vec3 dir = normalize(
            vec3((v_position.x) / u_width / zoom,
                 (v_position.y) / u_width / zoom, 2.0));
    mat4 rot3 = mat4(0.0, -1.0, 0.0, 0.0,
                     0.0, 0.0, -1.0, 0.0,
                     1.0, 0.0, 0.0, 0.0,
                     0.0, 0.0, 0.0, 1.0);

    mat4 rot = rot3 * u_move_matrix;
    vec3 new_eye = (rot * vec4(eye.xyz, 1.0)).xyz;
    vec3 new_dir = (rot * vec4(dir.xyz, 1.0)).xyz;
    light = (rot * vec4(light.xyz, 1.0)).xyz;

    mat4 old_rot = rot3 * u_old_move_matrix;
    vec3 old_eye = (old_rot * vec4(eye.xyz, 1.0)).xyz;
    vec3 old_dir = (old_rot * vec4(dir.xyz, 1.0)).xyz;

    float minpoint = 1.0;

    if (true) {
      // gauss
      for (int i = 0; i < 5; i++) {
        vec3 e1 = mix(old_eye, new_eye, minpoint);
        vec3 d1 = normalize(mix(old_dir, new_dir, minpoint));
        float delta = 0.001;
        vec3 e2 = mix(old_eye, new_eye, minpoint - delta);
        vec3 d2 = normalize(mix(old_dir, new_dir, minpoint - delta));
        float dist1 = line_dist(e1, d1, A, B-A);
        float dist2 = line_dist(e2, d2, A, B-A);
        minpoint = minpoint - dist1 / ((dist1-dist2)/delta);
      }
      minpoint = clamp(minpoint, 0.0, 1.0);
    }

    eye = mix(old_eye, new_eye, minpoint);
    dir = normalize(mix(old_dir, new_dir, minpoint));

    vec3 haze_color;
    float x = march(eye, dir, haze_color);

    vec3 hp = eye + dir * x;
    vec3 color = vec3(0);

    if (x < 20.0) {
      Material mat = getMaterial(hp, dir);

//    vec3 normal = getNormal(hp);
//    vec3 light_dir = light - hp;
//    float light_dist2 = dot(light_dir, light_dir) / 10.0;
//    light_dir = normalize(light_dir);
//    float l = dot(light_dir, normal) * 5.0;

//    l = max(l, 0.0);
//    l /= sqrt(light_dist2);
//    l += 0.1;  // ambient
//    color *= l;
//    color += color2 * 2.0;

//    vec3 reflection = reflect(dir, normal);
//    float l2 = max(dot(reflection, light_dir), 0.0);
//    color += vec3(200.0) * pow(l2, 60.0);
//    vec3 haze = vec3(0.01, 0.01, 0.01);
//    float haze_mix = x < 0.0 ? 0.0 : pow(0.95, x);
//    color = haze_mix * color + (1.0 - haze_mix) * haze;
//    color += haze_color;


//    color = sqrt(color);
//    gl_FragColor = vec4(color, 1.0);

    vec3 v = normalize(-dir);
    vec3 n = getNormal(hp);
    vec3 l = light - hp;
    vec3 ld = normalize(l);
    vec3 h = normalize(ld + v);
    vec3 r = normalize(reflect(dir, n));

    float NoV = abs(dot(n, v)) + 1e-5;
    float NoL = saturate(dot(n, ld));
    float NoH = saturate(dot(n, h));
    float LoH = saturate(dot(ld, h));

    vec3 baseColor = mat.color * 0.5;

    float intensity = 3.8;
    float indirectIntensity = 0.1;

    float linearRoughness = mat.roughness * mat.roughness;
    vec3 diffuseColor = (1.0 - mat.metallic) * baseColor.rgb;
    vec3 f0 = 0.04 * (1.0 - mat.metallic) + baseColor.rgb * mat.metallic;

    float attenuation = shadow(hp, l);

    // specular BRDF
    float D = D_GGX(linearRoughness, NoH, h);
    float V = V_SmithGGXCorrelated(linearRoughness, NoV, NoL);
    vec3  F = F_Schlick(f0, LoH);
    vec3 Fr = (D * V) * F;

    // diffuse BRDF
    vec3 Fd = diffuseColor * Fd_Burley(linearRoughness, NoV, NoL, LoH);

    color = Fd + Fr;
    color *= (intensity * attenuation * NoL) * vec3(0.98, 0.92, 0.89);

    // diffuse indirect
    vec3 indirectDiffuse = Irradiance_SphericalHarmonics(n) * Fd_Lambert();

    vec3 indirect_haze_color;
    float indirectHit = march2(hp + r * 0.1, r, indirect_haze_color);
//    float indirectHit = ray(hp + r * 0.1, r);
    vec3 indirectSpecular = vec3(0.5, 0.5, 0.5) + (dot(vec3(0,0,-1), r) + 1.0) * 0.2;
    vec3 indirectHitPosition = hp + r * indirectHit;
    if (indirectHit < 20.0) {
      Material indirectMaterial = getMaterial(indirectHitPosition, r);
      indirectSpecular = indirectMaterial.color + indirectMaterial.emission;
    }
    indirectSpecular += indirect_haze_color;

    // indirect contribution
    vec2 dfg = PrefilteredDFG_Karis(mat.roughness, NoV);
    vec3 specularColor = f0 * dfg.x + dfg.y;
    vec3 ibl = diffuseColor * indirectDiffuse + indirectSpecular * specularColor;

    color += ibl * indirectIntensity;

    color += mat.emission;
    }

//    vec3 haze = vec3(0.01, 0.01, 0.01);
//   float haze_mix = x > 20.0 ? 0.0 : pow(0.95, x);
//    color = haze_mix * color + (1.0 - haze_mix) * haze;
    color += haze_color;

    // Clip to white
    color += vec3(dot(max(color - vec3(1), vec3(0)), vec3(0.33)));
    // color += vec3(dot(max(color - vec3(1), 0.0), vec3(0.299, 0.587, 0.114)));
    // color += vec3(max(dot(color, vec3(0.299, 0.587, 0.114)) - 1.0, 0.0));

//    gl_FragColor = pow(vec4(color, 1.0), vec4(1.0/2.2));
    gl_FragColor = pow(vec4(color, 1.0), vec4(1.0/2.2));
}
	</script>


	<div id="popup_window" class="popup-window">
		<p id="popup_message"></p>
		<button title="OK" onclick="dismissPopupMessage()">OK</button>
		<br>
		<label title="Don't show this again. Click 'Restore Defaults' in Settings to re-enable.">
			<input type="checkbox" id="dont_show_again" name="dontshow" onclick="DontShowAgain(this.checked)">
			<span>Don't show this again</span>
		</label>
	</div>
	<div id="overlay" class="overlay"></div>

	<script type="application/javascript" src="main.js"></script>
</head>

<body>

<table>
	<tr>
		<td class="page-left">
			<div class="canvas-container">
				<canvas
						id="canvas_id"
						title="Blade Preview. Move mouse to swing. Click to Clash or Do Selected Effect (and to dismiss this Tooltip.)  Goto settings to change hilt model or toggle Mouse Swings mode (no swing on mouse move.)"
						onmousemove='mouse_move(event)' onmouseleave='mouse_leave(event)'
						onclick='AddClickedEffect()'>
				</canvas>
				<button
						id="ENLARGE"
						title="Click to toggle blade preview window size."
						style="position:absolute; bottom:10px; right:10px;">Enlarge</button>
			</div>
			<br>
			<button
					id="POWER_BUTTON"
					title="Turn the blade On and Off. Simulates ignition and retraction events on the blade."
					onclick="ClickPower()">Power</button>
			<button
					title="Simulates an impact hit on the blade. Triggers style code using EFFECT_CLASH."
					onclick="AddClash()">Clash</button>
			<button
					title="Simulates an impact hit on the tip of the blade. Triggers style code using EFFECT_STAB."
					onclick="AddStab()">Stab</button>
			<button
					title="Simulates a blaster bolt deflection impact hit on the blade. Triggers style code using EFFECT_BLAST."
					onclick="AddBlast()">Blast</button>
			<button
					title="Triggers style code using EFFECT_FORCE."
					onclick="AddForce()">Force</button>
			<button
					title="Simulates powering on the Proffieboard. Triggers style code Int<using EFFECT_BOOT."
					onclick="AddBoot()">Boot</button>
			<button
					title="Simulates changing presets. Triggers style code using EFFECT_NEWFONT."
					onclick="AddNewfont()">NewFont</button>
			<button
					title="Simulates &quot;pre-ignition&quot;; pressing the power button when there are preon.wav files available in the current font. Triggers style code using EFFECT_PREON."
					onclick="AddPreon()">Preon</button>

			<select
					id="LOCKUP" name="lockup"
					title="Choose a Lockup Type to begin lockup effect. Choose &quot;No Lockup&quot; to end lockup."
					onchange="OnLockupChange()" >
				<option value=LOCKUP_NONE>No lockup</option>
				<option value=LOCKUP_NORMAL>Lockup</option>
				<option value=LOCKUP_DRAG>Drag</option>
				<option value=LOCKUP_MELT>Melt</option>
				<option value=LOCKUP_LIGHTNING_BLOCK>LB</option>
			</select>

			<span class="more-menu-container">
      <select
			  id="more_effects_menu"
			  title="This menu contains all of the additional EFFECTS. Select one, then use the &quot;Do Selected Effect&quot; button to trigger the effect." >
      <option value="">Select More Effects</option>
      </select>
      <input
			  type=button id="do_selected" value="Do Selected Effect"
			  title="Press this to trigger the EFFECT selected in the More Effects menu.">
    </span>

			<script>
                // Get the menu and button elements
                const menu = FIND('more_effects_menu');
                const do_selected_button = FIND('do_selected');

                /* Add values from the enum builder to an array and sort alphabetically,
        excluding effects with dedicated buttons.*/
                const values = Object.entries(EFFECT_ENUM_BUILDER.value_to_name)
                    .sort((a, b) => a[1].localeCompare(b[1]))
                    .filter(([value]) => ![
                        EFFECT_CLASH,
                        EFFECT_STAB,
                        EFFECT_BLAST,
                        EFFECT_FORCE,
                        EFFECT_BOOT,
                        EFFECT_NEWFONT,
                        EFFECT_PREON,
                    ].includes(Number(value)));

                // Add sub-groups for the different categories of effects
                const generalEffectsMenu = document.createElement('optgroup');
                generalEffectsMenu.label = 'General Effects';
                const userEffectsMenu = document.createElement('optgroup');
                userEffectsMenu.label = 'User Effects';
                const blasterEffectsMenu = document.createElement('optgroup');
                blasterEffectsMenu.label = 'Blaster Effects';
                const gameEffectsMenu = document.createElement('optgroup');
                gameEffectsMenu.label = 'Game Effects';
                const errorMessagesMenu = document.createElement('optgroup');
                errorMessagesMenu.label = 'Error Messages';

                // Add sorted values to the menu and actions dictionary,
                for (const [value, name] of values) {
                    const nameWithoutEffect = name.replace(/^EFFECT_/, '');
                    const option = document.createElement('option');
                    option.value = value;
                    option.text = nameWithoutEffect;

                    // Check if the effect belongs to a certain category and add it to the corresponding sub-group
                    if (name.startsWith('EFFECT_GAME')) {
                        gameEffectsMenu.appendChild(option);
                    } else if (name.startsWith('EFFECT_USER')) {
                        userEffectsMenu.appendChild(option);
                    } else {
                        switch (Number(value)) {
                            case EFFECT_STUN:
                            case EFFECT_FIRE:
                            case EFFECT_CLIP_IN:
                            case EFFECT_CLIP_OUT:
                            case EFFECT_RELOAD:
                            case EFFECT_MODE:
                            case EFFECT_RANGE:
                            case EFFECT_EMPTY:
                            case EFFECT_FULL:
                            case EFFECT_JAM:
                            case EFFECT_UNJAM:
                            case EFFECT_PLI_ON:
                            case EFFECT_PLI_OFF:
                                blasterEffectsMenu.appendChild(option);
                                break;
                            case EFFECT_ERROR_IN_BLADE_ARRAY:
                            case EFFECT_ERROR_IN_FONT_DIRECTORY:
                            case EFFECT_FONT_DIRECTORY_NOT_FOUND:
                            case EFFECT_SD_CARD_NOT_FOUND:
                            case EFFECT_LOW_BATTERY:
                                errorMessagesMenu.appendChild(option);
                                break;
                            default:
                                generalEffectsMenu.appendChild(option);
                                break;
                        }
                    }
                }

                // Add the sub-groups to the main menu
                menu.appendChild(generalEffectsMenu);
                menu.appendChild(userEffectsMenu);
                menu.appendChild(blasterEffectsMenu);
                menu.appendChild(gameEffectsMenu);
                menu.appendChild(errorMessagesMenu);

                // Set up the event listener for the menu
                menu.addEventListener('change', function() {
                    // If the selected value is not the default, enable the button and set its action
                    if (menu.value !== '') {
                        do_selected_button.disabled = false;
                        do_selected_button.className = "button-on"
                        do_selected_button.onclick = function() {
                            AddClickedEffect();
                        };
                    } else {
                        // If the selected value is the default, disable the button
                        do_selected_button.disabled = true;
                        do_selected_button.onclick = null;
                        do_selected_button.className = "button-off"
                    }
                });
                // Disable the button initially
                do_selected_button.disabled = true;
                do_selected_button.className = "button-off"

                // What to do when preview saber area is clicked
                function AddClickedEffect() {
                    if (do_selected_button.disabled) {
                        AddClash();
                    } else {
                        blade.addEffect(menu.value, 0.0)
                    }
                };
			</script>

			<span class="variant-alt-container">
        <span class="variant-alt-controls">
          <span class="variant-label">Variant:</span>
          <input
				  id="VARIANT_SLIDER" name=varslider type=range class="variant-slider"
				  title="Slide to adjust the Variant value."
				  min="0" max="32768" step="1" value="0"
				  oninput="updateVariantValue(this.value)" >
          <input
				  id="VARIANT_MINUS" name=varminus type=button value="<"
				  title="Decreases the Variant value. Hold to increase speed of the changing value."
				  onmousedown="startAdjustingValue(-1, 'VARIANT_SLIDER')"
				  onmouseup="stopAdjustingValue()"
				  onmouseleave="stopAdjustingValue()" >
          <input
				  id="VARIANT_VALUE" name=varvalue type=input size="6" value="0"
				  title="The current Variant value. You can also type in this field for direct entry of a value."
				  oninput="updateVariantValue(this.value)" >
          <input
				  id="VARIANT_PLUS" name=varplus type=button value=">"
				  title="Increases the Variant value. Hold to increase speed of the changing value."
				  onmousedown="startAdjustingValue(1, 'VARIANT_SLIDER')"
				  onmouseup="stopAdjustingValue()"
				  onmouseleave="stopAdjustingValue()" >
        <span class="alt-label">Alt:</span>
          <input
				  id="ALT_MINUS" name=altminus type=button value="<"
				  title="Decreases the Alt value."
				  onclick="IncreaseAlt(-1)" >
          <input
				  id="ALT" name=alt type=input size="3" value="0"
				  title="The current Alt value. You can also type in this field for direct entry of a value." >
          <input
				  id="ALT_PLUS" name=altplus type=button value=">"
				  title="Increases the Alt value."
				  onclick="IncreaseAlt(1)" >
        </span>
      </span>
			<br>

			<span id="error_message" class="error-message"></span>

			<pre><textarea
					rows=10 cols=80 id=style wrap="off" class="textbox"
					title="Style and value editing area. Edits in here are committed by clicking the Submit button below." >
        </textarea></pre>

			<span>
          <button
				  class="submit-button" title="Submit what is currently in the text box above."
				  onclick="Run()">
            <i class="fa-solid fa-right-from-bracket"></i> Submit</button>
          <button
				  title="Adds StylePtr&lt;&gt;() wrapper and copies the style to the clipboard."
				  onclick="Copy()">
            <i class="fa-solid fa-copy"></i> Copy</button>
          <button
				  id="expand_button"
				  title="Reveals the &quot;under the hood&quot; code that the shorthand macro uses. This allows access to otherwise omitted arguments, and is only an active button when applicable."
				  onclick="DoExpand()">
            <i class="fa-solid fa-up-right-and-down-left-from-center"></i> Expand</button>
          <button
				  id="layerize_button"
				  title="Transforms nested version style code and breaks it out into more contemporary Layers format."
				  onclick="DoLayerize()">
            <i class="fa-solid fa-layer-group"></i> Layerize</button>
          <button
				  title="Converts arguments to their ARG version counterparts for use with ProffieOS Workbench or Edit Mode. See the &quot;ArgString&quot; tab below."
				  onclick="DoArgify()">
            <i class="fa-solid fa-list"></i> Argify</button>
          <button
				  id="ROTATE_BUTTON" title="Toggles animation of the preview blade above."
				  onclick="ClickRotate()">
            <i class="fa-solid fa-arrows-rotate"></i> Rotate</button>
          <button
				  title="Save the blade style to your computer."
				  onclick="ClickSave()">
            <i class="fa-solid fa-save"></i> Save</button>
          <button
				  id="SETTINGS_BUTTON" class="icon-button" title="Settings"
				  onclick="toggleSettingsPanel()">
            <i class="fa-solid fa-cog"></i> Settings</button>
      </span>

			<div id="settings_panel" class="settings-panel"
				 title="Turning on checkboxes saves your settings for future visits to the Style Editor page.">

				<button
						title="Close Settings Panel."
						class="close-settings" onclick="toggleSettingsPanel()">X</button>

				<h2 class="settings-header-label" title="Settings Panel">Settings</h2>

				<div class="settings-section">
					<h4 class="settings-section-label" title="These settings apply to the whole page.">General:</h4>

					<label title="Toggles a Dark Mode look to this page.">
						<input type="checkbox" id="DARK_BUTTON" name="darkmode" onclick="handleClick(this)">
						<span>Dark Mode</span>
					</label>

					<label title="Toggles pop-up tooltips On or Off.">
						<input type="checkbox" id="TIPS_BUTTON" name="tooltips" onclick="handleClick(this)">
						<span >Tool Tips</span>
					</label>

				</div>
				<div class="settings-section">
					<h4 class="settings-section-label" title="These settings apply to the blade preview above.">Blade Preview:</h4>

					<label title="Toggles the Graflex Hilt model in the preview.">
						<input type="checkbox" id="GRAFLEX_BUTTON" name="graflex" onclick="handleClick(this)">
						<span>Graflex Hilt</span>
					</label>

					<label title="Toggles the option for swinging around the preview blade above using mouse control.">
						<input type="checkbox" id="MOUSESWINGS_BUTTON" name="mouseswings" onclick="handleClick(this)">
						<span>Disable mouse swings</span>
					</label>

					<label title="Toggles automated swing emulation on the preview blade while at rest (ie: shown when using SwingSpeed<>). Swinging while hovering over the preview area always shows swing blade effects according to movements.">
						<input type="checkbox" id="AUTOSWING_BUTTON" name="autoswing" onclick="handleClick(this)">
						<span>Swing Emulation</span>
					</label>

					<label title="Simulates In-Hilt LED blades, (no addressable pixel effects).">
						<input type="checkbox" id="INHILT_BUTTON" name="inhilt" onclick="handleClick(this)">
						<span>Inhilt LED</span>
					</label>

					<label title="Provides &quot;snapshots&quot; of rapidly moving things to see what they look like without fast movement. *Note* Graflex hilt model probably won't be able to render with this on.">
						<input type="checkbox" id="SLOW_BUTTON" name="slow" onclick="handleClick(this)">
						<span>Slow</span>
					</label>
				</div>
			</div>
			<script>

                function toggleSettingsPanel() {
                    const settingsButton = FIND("SETTINGS_BUTTON");
                    const settingsPanel = FIND("settings_panel");

                    settingsPanel.classList.toggle("show");

                    // Mouseleave event listener
                    if (settingsPanel.classList.contains("show")) {
                        let timeoutId = null;
                        settingsPanel.addEventListener("mouseleave", function(e) {
                            timeoutId = setTimeout(function() {
                                settingsPanel.classList.remove("show");
                            }, 1000);
                        });
                        settingsPanel.addEventListener("mouseenter", function(e) {
                            if (timeoutId) {
                                clearTimeout(timeoutId);
                                timeoutId = null;
                            }
                        });
                    }
                }

                // Call the onPageLoad function when the page is loaded
                window.addEventListener('DOMContentLoaded', onPageLoad);

                const all_saved_states = [];
                const state_by_checkbox = new Map();
                const body = document.querySelector("body");
                let structuredView;

                /* Settings buttons saved as local storage */
                function getSavedState(buttonState, defaultValue) {
                    const value = localStorage.getItem(buttonState);
                    console.log("Retrieved SavedState for " + buttonState + ": " + value);

                    if (value === null) {
                        return defaultValue;
                    }
                    return value !== "false";
                }

                function saveState(buttonState, settingIsOn) {
                    localStorage.setItem(buttonState, settingIsOn);
                }

                class SavedState {
                    constructor(name, def, update_function) {
                        this.name = name;
                        this.def = def;
                        this.update_function = update_function;
                        all_saved_states.push(this);
                        const checkbox = FIND(this.name.toUpperCase()+"_BUTTON");
                        state_by_checkbox.set(checkbox, this);
                    }
                    onload() {
                        this.set(getSavedState(this.name+"Save", this.def));
                    }
                    set(value) {
                        this.value = value;
                        FIND(this.name.toUpperCase()+"_BUTTON").checked = value ? true : false;
                        saveState(this.name+"Save", value);
                        this.update_function(value);
                    }
                    get() { return this.value; }
                }

                const darkState = new SavedState("dark", false, (on) => {
                    body.classList.toggle("dark-mode", on);
                    structuredView.classList.toggle("dark-mode", on);
                });

                const tipsState = new SavedState("tips", true, (on) => {
                    if (on) {
                        const elementsWithDataTitles = document.querySelectorAll("[data-title]");
                        elementsWithDataTitles.forEach((element) => {
                            element.setAttribute("title", element.dataset.title);
                            element.removeAttribute("data-title");
                        });
                    } else {
                        const elementsWithTitles = document.querySelectorAll("[title]");
                        elementsWithTitles.forEach((element) => {
                            element.dataset.title = element.getAttribute("title");
                            element.removeAttribute("title");
                        });
                    }
                });
                const graflexState = new SavedState("graflex", true, (on) => {
                    compile();
                });
                const mouseswingsState = new SavedState("mouseswings", false, (on) => {
                });
                const autoswingState = new SavedState("autoswing", true, (on) => {
                });
                const inhiltState = new SavedState("inhilt", false, (on) => {
                    STATE_NUM_LEDS = on ? 1 : 144;
                });
                const slowState = new SavedState("slow", false, (on) => {
                    framesPerUpdate = on ? 10 : 0;
                    time_factor = framesPerUpdate == 0 ? 1000 : (500 / framesPerUpdate)
                });

                function onPageLoad() {
                    initGL();
                    structuredView = FIND("structured_view");
                    all_saved_states.forEach(state => {
                        state.onload();
                    });
                }

                function handleClick(checkbox) {
                    var state = state_by_checkbox.get(checkbox);
                    state.set(!state.get());
                }

			</script>
			<br>
			<div id=TABS>
				<div id=TABLINKS class=tab></div>
				<div id=TABBODIES class=tabcontainer></div>
			</div>

		</td>
		<td class="page-right">
			<div id="structured_view" class="structured-view">
				<span class="structured-view-label">Structured view, click to edit.</span>
				<div id="pp" class="pp"></div>
			</div>
		</td>
	</tr>
</table>
</body>
</html>